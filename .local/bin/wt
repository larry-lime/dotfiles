#!/bin/zsh

# Git Worktree Switcher for tmux
# Find a git worktree to anchor against. Prefer the current dir, but fall back to any
# immediate subdirectory (useful when the cwd is a parent folder holding multiple worktrees).
REPO_DIR=""
if git rev-parse --is-inside-work-tree > /dev/null 2>&1; then
    REPO_DIR="$PWD"
else
    for dir in "$PWD"/*; do
        [[ -d "$dir" ]] || continue
        if git -C "$dir" rev-parse --is-inside-work-tree > /dev/null 2>&1; then
            REPO_DIR="$dir"
            break
        fi
    done
fi

if [[ -z "$REPO_DIR" ]]; then
    echo "Error: Not in a git repository and no worktrees found"
    sleep 1
    exit 1
fi

# Get all worktrees with branch names
worktrees=$(git -C "$REPO_DIR" worktree list --porcelain | awk '
    /^worktree /{wt_full_path=$2; wt_display_name=$2; branch_name=""; next}
    /^branch /{branch_name=$2; sub("refs/heads/","",branch_name); next}
    /^detached/{branch_name="(detached)"; next}
    /^bare/{branch_name="(bare)"; next}
    /^$/{
        if (wt_full_path!=""){
            b=(branch_name=="" ? "(no-branch)" : branch_name);
            sub(".*/", "", wt_display_name); # Extract basename for display
            print wt_full_path "\t" wt_display_name "\t" b;
            wt_full_path=""; wt_display_name=""; branch_name=""
        }
        next
    }
    END{
        if (wt_full_path!=""){
            b=(branch_name=="" ? "(no-branch)" : branch_name);
            sub(".*/", "", wt_display_name);
            print wt_full_path "\t" wt_display_name "\t" b;
        }
    }
')

if [[ -z "$worktrees" ]]; then
    echo "No worktrees found"
    sleep 1
    exit 1
fi

# Use plain fzf (we're already in a popup)
selected=$(echo "$worktrees" |
    fzf-tmux -p 40%,70%\
        --reverse \
        --prompt="Worktree > " \
        --header="Select worktree" \
        --delimiter="\t" \
        --with-nth=2,3 )

fzf_status=$?

# Exit quietly if picker was cancelled
if [[ $fzf_status -ne 0 || -z "$selected" ]]; then
    exit 0
fi

selected_path=$(echo "$selected" | cut -f1)

tmux send-keys -t "$TMUX_PANE" "cd \"$selected_path\"" C-m

